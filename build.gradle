plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

ext.getCurrentBranchName = {
    new ByteArrayOutputStream().withStream { os ->
        exec {
            executable = "git"
            args = ["rev-parse", "--abbrev-ref", "HEAD"]
            standardOutput = os
        }
        return os.toString().trim()
    }
}

group "net.jeikobu.kotomi"

def releaseNumber = "1.1"
def isSnapshot = !getCurrentBranchName().equals("master")

version = releaseNumber
if (isSnapshot) version = version + "-SNAPSHOT"

def buildNumber = "LOCAL_DEV_BUILD"
if (project.hasProperty("github")) {
    buildNumber = System.getenv("GITHUB_RUN_NUMBER")
}

sourceCompatibility = 1.8

repositories {
    maven {
        url "https://nexus.jeikobu.net/repository/maven-repositories/"
    }
}

dependencies {
    api 'net.dv8tion:JDA:3.8.3_464'
    api 'org.tinylog:tinylog:1.3.5'
    api 'org.tinylog:slf4j-binding:1.3.5'
    api 'org.jetbrains.exposed:exposed:0.14.1'

    implementation "org.cfg4j:cfg4j-core:4.4.0"
    implementation 'org.joda:joda-convert:2.1.1'
    implementation 'com.google.guava:guava:26.0-jre'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
    implementation "org.codehaus.groovy:groovy-all:2.4.15"

    testImplementation "org.spockframework:spock-core:1.1-groovy-2.4"
    testImplementation "org.hamcrest:hamcrest-core:1.3"   // only necessary if Hamcrest matchers are used
    testRuntimeOnly "net.bytebuddy:byte-buddy:1.6.5"  // allows mocking of classes (in addition to interfaces)
    testRuntimeOnly "org.objenesis:objenesis:2.5.1"  // allows mocking of classes without default constructor (together with CGLIB)
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourceJar
}

jar {
    manifest {
        attributes(
                "Created-By": "Shindou Jeikobu",
                "Date": new Date(),
                "Implementation-Version": version + "-" + buildNumber,
                "Build-Number": buildNumber
        )
    }
}

publishing {
    afterEvaluate {
        repositories {
            maven {
                credentials {
                    if (project.hasProperty("github")) {
                        username System.getenv("nexusUsername")
                        password System.getenv("nexusPassword")
                    } else {
                        username project.nexusUsername
                        password project.nexusPassword
                    }
                }
                def releasesRepoUrl = "https://nexus.jeikobu.net/repository/maven-releases/"
                def snapshotsRepoUrl = "https://nexus.jeikobu.net/repository/maven-snapshots/"
                url version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            }
        }

        publications {
            mavenJava(MavenPublication) {
                groupId = group
                artifactId 'base'

                from components.java

                artifact sourceJar {
                    classifier "sources"
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}